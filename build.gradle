import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'

group = 'com.github.twitch4j'
version = '1.0.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// Repositories
repositories {
	mavenCentral()
	maven {
		name = 'spigotmc-repo'
		url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}
}

// Dependencies
dependencies {
	// Minecraft server environment (provided)
	compileOnly group: 'org.bukkit', name: 'bukkit', version: '1.15.2-R0.1-SNAPSHOT'

	// Twitch4J
	implementation group: 'com.github.twitch4j', name: 'twitch4j', version: '1.12.0'

	// Logging
	implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'
}

// Ensure plugin.yml has the version defined here
processResources {
	from(sourceSets.main.resources.srcDirs) {
		duplicatesStrategy DuplicatesStrategy.INCLUDE // config, logback, and plugin yml files are not dupes - https://github.com/gradle/gradle/issues/17236
		filter ReplaceTokens, tokens: [version: version]
	}
}

// Create a fat jar at: build/libs/
jar {
	from {
		duplicatesStrategy DuplicatesStrategy.INCLUDE // primarily META-INF duplicates
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}
